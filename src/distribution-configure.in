AC_PREREQ(2.8)
AC_INIT(lyskom-rest.el)

AC_ARG_WITH(emacs, 
[ --with-emacs=EMACS            specify which emacs to use (EMACS=emacs)],
[ emacs="${withval}"],
[ AC_CHECK_PROGS(emacs, [emacs xemacs]) ]
)

AC_ARG_WITH(lispdir,
[ --with-lispdir=DIR            specify directory to install Lisp code in],
[ lispdir="${withval}" ],
[ lispdir="" ])

AC_ARG_ENABLE(language,
[ --enable-language=LANG         include support for language LANG ],
[ case "${enableval}" in 
    sv|swedish) val="swedish" ;;
    en|english) val="english" ;;
    no) AC_MSG_ERROR([You cannot disable a language]) ;;
    *) AC_MSG_ERROR([Argument \`--enable-language=$enableval' is invalid.
The language must be one of swedish or english.]) ;;
  esac

  languages="${languages} ${val}"
],
[ languages="swedish english" ]
)


AC_ARG_WITH(default-language,
[ --with-default-language=LANG  set default language to LANG (default swedish)],
[ case "${withval}" in
    sv|swedish) val="swedish" ;;
    en|english) val="english" ;;
    *) AC_MSG_ERROR([Argument \`--with-default-language=$withval' is invalid.
The default language must be one of \`swedish' or \`english'.]);
  esac
  languages="`echo ${languages} | sed -e \"s/ *${val} *//g\"`"
  languages="${val} ${languages}"
])




if test -z "${lispdir}"
then

   eval `${emacs} -batch -l ./autoconf.el -f extract-autoconf-data 2>/dev/null`
   if test -z "${xlispdir}"
   then
       AC_MSG_ERROR([No Lisp directory was found or specified with --with-lispdir.])
   fi
   lispdir="${xlispdir}"
fi

echo 
echo "LysKOM elisp-client configuration" 
echo "  Path prefix:               $prefix"
echo "  Emacs:                     $emacs" 
echo "  Installation directory:    $lispdir" 
echo "  Documentation directory:   $infodir" 
echo "  Languages (default first): $languages" 
echo 

AC_SUBST(emacs)
AC_SUBST(prefix)
AC_SUBST(infodir)
AC_SUBST(lispdir)
AC_SUBST(languages)

AC_OUTPUT(Makefile)
